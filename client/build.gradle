plugins {
    id 'bitflask.java-application-conventions'
}

def picocliVersion = '4.7.4'

dependencies {
    implementation project(':resp')
    implementation project(':common')

    implementation "info.picocli:picocli:${picocliVersion}"
    annotationProcessor "info.picocli:picocli-codegen:${picocliVersion}"
}

compileJava {
    // Picoli annotation processor at compile time
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

application {
    // Define the main class for the application.
    mainClass.set('dev.sbutler.bitflask.client.Client')

    applicationDefaultJvmArgs = ['--enable-preview',
                                 '--add-modules', 'jdk.incubator.concurrent']
}

run {
    // Allow standard input while running client via gradle task
    standardInput = System.in
}

jar {
    manifest {
        attributes(
                'Main-Class': 'dev.sbutler.bitflask.client.Client'
        )
    }
}
