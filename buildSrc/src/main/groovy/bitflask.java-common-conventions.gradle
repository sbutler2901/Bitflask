/** Common gradle conventions shared by all Bitflask application and library subprojects. */

plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf'
}

group 'dev.sbutler'
version '1.0'

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

def guiceVersion = '7.0.0'
def guavaVersion = '32.1.2'
def mugVersion = '6.6'
def floggerVersion = '0.7.4'
def grpcVersion = '1.58.0'
def protobufVersion = '3.24.3'
def junitVersion = '5.10.0'
def truthVersion = '1.1.5'
def mockitoVersion = '5.5.0'

dependencies {
    implementation "com.google.inject:guice:${guiceVersion}"
    implementation "com.google.inject.extensions:guice-assistedinject:${guiceVersion}"
    implementation "com.google.inject.extensions:guice-throwingproviders:${guiceVersion}"

    implementation "com.google.guava:guava:${guavaVersion}-jre"

    implementation "com.google.mug:mug:${mugVersion}"
    implementation "com.google.mug:mug-guava:${mugVersion}"

    implementation "com.google.flogger:flogger:${floggerVersion}"
    implementation "com.google.flogger:flogger-log4j2-backend:${floggerVersion}"

    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    // Testing

    testImplementation "com.google.guava:guava-testlib:${guavaVersion}-jre"

    testImplementation "com.google.truth:truth:${truthVersion}"
    testImplementation "com.google.truth.extensions:truth-java8-extension:${truthVersion}"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"

    testImplementation(platform("org.junit:junit-bom:${junitVersion}"))
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll(['--enable-preview'])
}

tasks.withType(Test).configureEach {
    jvmArgs += "--enable-preview"
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += "--enable-preview"
}

// Gradle protobuf plugin
protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
