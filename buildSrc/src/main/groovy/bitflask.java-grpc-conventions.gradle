/** Common gradle conventions shared by all Bitflask subprojects that use grpc. */

plugins {
    // NOTE: external plugin added as implementation dependency in buildSrc/build.gradle
    id 'com.google.protobuf'
}

def grpcVersion = '1.56.0'
def protobufVersion = '3.23.2'

dependencies {
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    // Testing
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}

// Gradle protobuf plugin
protobuf {
    protoc {
        // The artifact spec for the Protobuf Compiler
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
